<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.8">
  <POU Name="FB_PendantControl" Id="{e2b10f6c-d16d-4403-9ebe-8d8a369c8dbd}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_PendantControl
VAR_INPUT
END_VAR
VAR
	CoverControl		: I_MirrorCovers;
	fbBrake				: I_Brake;
	HydraulicsControl	: REFERENCE TO FB_HydraulicsControl;
	Focuscontrol 		: I_Focus;
	ElevationControl	: REFERENCE TO FB_ElevationControl;
	AzimuthControl 		: REFERENCE TO FB_AzimuthControl;
	DerotatorControl	: REFERENCE TO FB_DerotatorControl;
	TelescopeControl	: REFERENCE TO FB_TelescopeControl;
	comm				: I_Comm;

	// program selection of the pendant control
	Selection:	UINT;
	// error on the pendant control
	Error:		BOOL;
	horn:			BOOL;
	FocusEnable: 		R_TRIG;
	DerotatorEnable:	FB_ButtonEnable;
	ElevationEnable: 	FB_ButtonEnable;
	AzimuthEnable: 		FB_ButtonEnable;
	TelescopeControlEnable: 	FB_ButtonEnable;
	HydraulicsEnable: 	FB_ButtonEnable;
	TrackingEnable:			FB_ButtonEnable;
	ResetDelay:	TON;
	TwinCATRestart: TC_Restart;
	up_trigger:	R_TRIG;
	down_trigger:	R_TRIG;	
	// manual or automatic operation of the telescope
	telescope_mode : E_TelescopeMode;
	
	reset_trigger		: R_TRIG;

	//DIN17 2.0
	BCD0		AT%I*: BOOL;
	//DIN18 2.1
	BCD2		AT%I*: BOOL;
	//DIN19 2.2
	BCD4		AT%I*: BOOL;
	//DIN20 2.3
	BCD8		AT%I*: BOOL;
	//DIN21 2.4
	reset_switch	AT%I*: BOOL;
	//DIN22 2.5
	direction_up	AT%I*: BOOL;
	//DIN23 2.6
	direction_down	AT%I*: BOOL;
	//DIN24 2.7
	key_switch		AT%I*: BOOL;
	//DIN25 3.0 (enable)
	start_switch	AT%I*: BOOL;
	//DIN26 3.1
	direction_left	AT%I*: BOOL;
	//DIN27 3.2
	direction_right	AT%I*: BOOL;
	
	// Outputs
	
	//DOUT9 1.0
	lamp_up			AT%Q*: BOOL;
	//DOUT10 1.1
	lamp_down			AT%Q*: BOOL;
	//DOUT11 1.2
	lamp_right			AT%Q*: BOOL;
	//DOUT12 1.3
	lamp_left			AT%Q*: BOOL;
	//DOUT13 1.4
	lamp_error			AT%Q*: BOOL;	
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* 
This program is responsible for the manual control of the telescope.
all movable parts can be controlled from here. This is mainly for testing and
maintenance.
WARNING! Some of the safety routines are disabled, when operating the telescope manually.
*)
// calculate the value of the selector
Selection := 1*BOOL_TO_UINT(BCD0) + 
			 2*BOOL_TO_UINT(BCD2) + 
			 4*BOOL_TO_UINT(BCD4) +
			 8*BOOL_TO_UINT(BCD8);

// detect disconnection of the manual controls (should also trigger emergency stop).			 
Error := (Selection = 0);

// disable all the lamps
lamp_left := FALSE;
lamp_right := FALSE;
lamp_up := FALSE;
lamp_down := FALSE;
horn := FALSE;

up_trigger(CLK := direction_up);
down_trigger(CLK := direction_down);
//R_Enable(CLK := enable);
// the error lamp is the same as the error lamp of the main panel
lamp_error := lamp_error;


// determine the operation of the telescope based on the switch set
IF key_switch THEN
	telescope_mode := E_TelescopeMode.manual;
ELSE
	telescope_mode := E_TelescopeMode.automatic;
END_IF

// reset pushed?
reset_trigger(CLK := reset_switch);

// enable only the selected item and connect lamps and buttons accordingly
CASE Selection OF
	1, 2, 3:	// reserved for Cover 1-3 control
		lamp_up := CoverControl.IsCoverOpen(Selection);
		lamp_down := CoverControl.IsCoverClosed(Selection);
		IF key_switch THEN
			IF direction_up AND CoverControl.IsCoverOpen(Selection) THEN
				CoverControl.OpenCover(Selection);
			ELSIF direction_down AND CoverControl.IsCoverClosed(Selection) THEN
				CoverControl.CloseCover(Selection);
			END_IF
			lamp_error := CoverControl.HasError(Selection);	
		ELSE
			IF direction_down THEN 
				CoverControl.CloseCovers();
			ELSIF direction_up THEN
				CoverControl.OpenCovers();
			END_IF		
		END_IF
		
	4:	// Nasmyth / mirror M3 control
		
	5:	// Focus Control
		lamp_up := FocusControl.Ready AND NOT FocusControl.InFarLimit;
		lamp_down := FocusControl.Ready AND NOT FocusControl.InNearLimit;
		IF key_switch THEN
			FocusEnable(CLK := start_switch);
			IF FocusEnable.Q THEN
				FocusControl.Enable := NOT FocusControl.Enable;
			END_IF
			FocusControl.MoveOut(direction_up);
			FocusControl.MoveIn(direction_down);
			IF reset_trigger.Q THEN
				Focuscontrol.Reset();
			END_IF
			lamp_error := FocusControl.Error;
		END_IF
							
	6:	// Derotator control
		lamp_left  := DerotatorControl.IsReady() AND NOT DerotatorControl.IsInMinLimit();
		lamp_right := DerotatorControl.IsReady() AND NOT DerotatorControl.IsInMaxLimit();
		IF key_switch THEN
			DerotatorEnable(SET := start_switch, Q := DerotatorControl.enable);
			DerotatorControl.MoveLeft(direction_left AND NOT DerotatorControl.inDigitalInputs.1);
			DerotatorControl.MoveRight(direction_right AND NOT DerotatorControl.inDigitalInputs.0);
			DerotatorControl.reset := reset_switch;
			lamp_error := DerotatorControl.Error;
		END_IF
							
	7:	// Elevation Control
		
		ElevationControl.up := direction_up AND ElevationControl.inDigitalInputs.0;
		ElevationControl.down := direction_down AND ElevationControl.inDigitalInputs.1;
		IF key_switch THEN
			ElevationEnable(
				SET := start_switch, 
				Q := ElevationControl.enable);	
		END_IF
		ElevationControl.reset := reset_switch;
		lamp_up := ElevationControl.inDigitalInputs.0;
		lamp_down := ElevationControl.inDigitalInputs.1;
		lamp_error := ElevationControl.error;
		
	8:	// Azimuth Control
		lamp_left := AzimuthControl.inDigitalInputs.0; // NOT GVL_Telescope.azimut_limit_switch;
		lamp_right := AzimuthControl.inDigitalInputs.1; // NOT GVL_Telescope.azimut_limit_switch;
		AzimuthControl.left := direction_left AND AzimuthControl.inDigitalInputs.0;
		AzimuthControl.right := direction_right AND AzimuthControl.inDigitalInputs.1;
		AzimuthControl.reset := reset_switch;
		IF key_switch THEN
			AzimuthEnable(
				SET := start_switch, 
				Q := AzimuthControl.enable);
			lamp_error := AzimuthControl.error;
		END_IF
		
	9: 	// Dome control
		
	10:	// FilterWheel control
		
	11: lamp_up := ElevationControl.torque > 0.0;
		lamp_down := ElevationControl.torque < 0.0;
		
	12:	// manual Telescope Control
		IF key_switch THEN
			lamp_up := TelescopeControl.power;
			lamp_down := TelescopeControl.power;
			lamp_left := TelescopeControl.power;
			lamp_right := TelescopeControl.power;
	
			TelescopeControl.gohome := direction_down;
			TelescopeControl.park := direction_up;
			TelescopeControl.goto := direction_right;
			TrackingEnable(SET := start_switch, 
							RESET := reset_switch, 
							Q := TelescopeControl.track);
			TelescopeControl.reset := reset_switch;
			Telescopecontrol.stop := direction_left;	
			lamp_error := Telescopecontrol.error;
		END_IF
		
	13: // Telescope Control
		TelescopeControlEnable(
			SET := start_switch, 
			Q := TelescopeControl.power);
		TelescopeControl.reset := reset_switch;
		IF key_switch THEN
			lamp_up := ElevationControl.inDigitalInputs.0;
			lamp_down := ElevationControl.inDigitalInputs.1;
			lamp_left := AzimuthControl.inDigitalInputs.0;
			lamp_right := AzimuthControl.inDigitalInputs.1;
			ElevationControl.up := direction_up AND ElevationControl.inDigitalInputs.0;
			ElevationControl.down := direction_down AND ElevationControl.inDigitalInputs.1;
			AzimuthControl.left := direction_left AND AzimuthControl.inDigitalInputs.0;
			AzimuthControl.right := direction_right AND AzimuthControl.inDigitalInputs.1;
			TelescopeControl.rightascension := TelescopeControl.rightascension_calc;
			TelescopeControl.declination := TelescopeControl.declination_calc;
		ELSE
			lamp_up := ElevationControl.enable;
			lamp_down := ElevationControl.enable;
			lamp_left := AzimuthControl.enable;
			lamp_right := AzimuthControl.enable;
			IF direction_up THEN 
				Telescopecontrol.elevation_offset := Telescopecontrol.elevation_offset + 10.0/3600.0; 
			ELSIF direction_down THEN 
				Telescopecontrol.elevation_offset := Telescopecontrol.elevation_offset - 10.0/3600.0; 
			END_IF;
			IF direction_left THEN 
				Telescopecontrol.azimuth_offset := Telescopecontrol.azimuth_offset - 10.0/3600.0; 
			ELSIF direction_right THEN 
				Telescopecontrol.azimuth_offset := Telescopecontrol.azimuth_offset + 10.0/3600.0; 
			END_IF;
		END_IF
		ResetDelay(IN := reset_switch, PT := T#3S);
		// wait for suction pump to stop
		TwinCATRestart(NETID := '', RESTART := ResetDelay.Q AND NOT HydraulicsControl.bSuctionRunning, TMOUT := T#60S);
	
	14: horn := key_switch;
		lamp_up := key_switch;
		lamp_down := key_switch;
		lamp_left := key_switch;
		lamp_right := key_switch;
	
	15:	// Hydraulics control
		lamp_up := HydraulicsControl.bOilPressureOK AND NOT fbBrake.BrakeOpen;
		lamp_down := HydraulicsControl.bOilPressureOK AND fbBrake.BrakeOpen;
		lamp_left := HydraulicsControl.bPumpRunning;
		lamp_right := HydraulicsControl.bSuctionRunning;
		IF key_switch THEN
			HydraulicsEnable(
				SET := start_switch, 
				Q := Hydraulicscontrol.bEnable);
			Hydraulicscontrol.bReset := reset_switch;
			
			IF direction_up THEN
				fbBrake.OpenBrake();
			END_IF
			IF direction_down THEN
				fbBrake.CloseBrake();
			END_IF
			HydraulicsControl.bManualSuction := direction_right;
			HydraulicsControl.bManualPumpStop := direction_left;
			lamp_error := HydraulicsControl.bError;
		END_IF
END_CASE
]]></ST>
    </Implementation>
    <Method Name="FB_Init" Id="{377c4b19-44fb-4929-801b-86205f66218e}">
      <Declaration><![CDATA[//FB_Init is always available implicitly and it is used primarily for initialization.
//The return value is not evaluated. For a specific influence, you can also declare the
//methods explicitly and provide additional code there with the standard initialization
//code. You can evaluate the return value.
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)
	cover			: I_MirrorCovers;
	brake			: I_Brake;
	hydraulics 		: REFERENCE TO FB_HydraulicsControl;
	focus			: I_Focus;
	elevation		: REFERENCE TO FB_ElevationControl;
	azimuth			: REFERENCE TO FB_AzimuthControl;
	derotator		: REFERENCE TO FB_DerotatorControl;
	telescope		: REFERENCE TO FB_TelescopeControl;
	comm			: I_Comm;   
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbBrake := brake;
HydraulicsControl REF= hydraulics;
CoverControl := cover;
FocusControl := focus;
ElevationControl REF= elevation;
AzimuthControl REF= azimuth;
DerotatorControl REF= derotator;
TelescopeControl REF= telescope;
THIS^.comm := comm;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_PendantControl">
      <LineId Id="1" Count="34" />
      <LineId Id="241" Count="0" />
      <LineId Id="240" Count="0" />
      <LineId Id="242" Count="0" />
      <LineId Id="36" Count="6" />
      <LineId Id="237" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="238" Count="0" />
      <LineId Id="47" Count="0" />
      <LineId Id="239" Count="0" />
      <LineId Id="48" Count="3" />
      <LineId Id="53" Count="3" />
      <LineId Id="94" Count="7" />
      <LineId Id="248" Count="2" />
      <LineId Id="102" Count="1" />
      <LineId Id="243" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="244" Count="0" />
      <LineId Id="105" Count="130" />
    </LineIds>
    <LineIds Name="FB_PendantControl.FB_Init">
      <LineId Id="9" Count="0" />
      <LineId Id="1" Count="5" />
      <LineId Id="8" Count="0" />
      <LineId Id="7" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>