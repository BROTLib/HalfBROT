<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.8">
  <POU Name="FB_DerotatorControl" Id="{acfc3cc1-a4bd-4873-ba58-50b0c54d8bf5}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_DerotatorControl
VAR_INPUT
	enable			: BOOL;		// enable the derotator
	reset			: BOOL;		// reset an error
	position		: LREAL;	// set the absolute position
	velocity		: LREAL := 10.0;	// derotator velocity
	moveAxis		: BOOL;		// move to given position
	homeAxis		: BOOL;		// calibrate the axis
	tracking		: BOOL;		// tracking flag
	stopAxis		: BOOL;		// stop the Axis
	bSoEReset		: BOOL;		// diagnostic error code reset
	calibration_position	: LREAL := 438.4623; // calibration in positive direction (was 460.0)
	min_position	: LREAL;
	max_position	: LREAL;
END_VAR
VAR_OUTPUT
	Error			: BOOL;		// error flag
	ErrorID			: UDINT;	// nc error id
	actual_position	: LREAL;	// actual position of the derotator
	Calibrated		: BOOL;		// calibrated flag
	Ready			: BOOL;		// axis ready flag
	slewtime		: LREAL;	// position to move the axis to
END_VAR
VAR
	comm			: I_Comm;	
	TorqueError		: TON := (PT := T#3S);	// trigger for the torque error
	DerotatorAxis	: FB_Axis2;	// axis function block
	DerotatorEvent	: FB_EventLog;
	TorqueWarning	: FB_EventLog;	// torque warning event
	CalibrationEvent: FB_EventLog;	// torque calibration event
	DerotatorDiagnosticEvent: FB_EventLog;	// diagnostic low-level event
	derotator_torque: REAL;	// percentage of the derotator torque
	ReadParameter	: MC_ReadParameter;	// read slewtime
	fbSoEReset		: FB_SoEReset;
	DiagnosticError	: BOOL;
	left			: BOOL;		// decrease derotator angle
	right			: BOOL;		// increase derotator angle
	
	digital_inputs	AT%I*	: WORD;		// digitale inputs der Endlagen
	diagnostic		AT%I*	: UDINT;	// diagnostic number
	torque			AT%I*	: INT;		// derotator torque feedback
	sto				AT%I*	: BOOL;		// STO
	axis_ref				: AXIS_REF;	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*
Selection 6
Homing in Endlage:

https://infosys.beckhoff.com/index.php?content=../content/1031/ax5000_usermanual/html/ax5000_homing_hardware_end_position.htm&id=12221
NC-Interface:
PlcToNc-Achsinterface, Bit 8 namens AcceptBlockedDriveSignal im nDeCtrlDWord

SPS-Interface:
TcNc-Lib, siehe SPS-Funktion AxisSetAcceptBlockedDriveSignal in der TwinCAT  PLC Library: NC.
*)

position := LIMIT(min_position, position, max_position);

IF enable AND NOT DerotatorAxis.Calibrated THEN
	HomeAxis := TRUE;
END_IF

IF DerotatorAxis.Calibrated THEN
	HomeAxis := FALSE;
	Calibrated := DerotatorAxis.Calibrated;
END_IF
	
IF HomeAxis THEN
	position := calibration_position;
END_IF

DeRotatorAxis(
	enable := 			enable,
	Reset := 			Reset,
	MoveAxis :=			MoveAxis,
	HomeAxis :=			HomeAxis,
	StopAxis :=			StopAxis,
	HomingMode :=		MC_ForceCalibration,
	Position := 		position, 
	Velocity :=			Velocity,
	Enable_Positive := 	NOT digital_inputs.0,
	Enable_Negative := 	NOT digital_inputs.1,
	Jog_Forward := 		right,
	Jog_Backwards := 	left,
	bCalibrationCam :=	digital_inputs.0,
	isModuloAxis :=		FALSE,
	Tracking :=			Tracking,
	ActualPosition => 	actual_position,
	Error => 			Error,
	ErrorID => 			ErrorID,
	AxisRef :=			axis_ref);
	
IF DeRotatorAxis.MoveDone OR Reset OR Error THEN
	MoveAxis := FALSE;
END_IF

IF DeRotatorAxis.HomeDone OR Reset OR Error THEN
	HomeAxis := FALSE;
END_IF
	
IF DeRotatorAxis.ResetDone THEN
	reset := FALSE;
END_IF

IF DeRotatorAxis.StopDone THEN
	StopAxis := FALSE;
END_IF

DiagnosticError := FALSE;
IF diagnostic < 16#D012 AND diagnostic > 16#D014 THEN
	DiagnosticError := TRUE;
	ErrorID := diagnostic;
	bSoeReset := reset;
END_IF

Ready := DeRotatorAxis.Ready;

ReadParameter(	Axis := axis_ref, 
				Enable := DeRotatorAxis.InMotion, 
				ParameterNumber := MC_AxisParameter.AxisRemainingTimeToGo, 
				ReadMode := E_ReadMode.READMODE_CYCLIC);
				
IF ReadParameter.Valid THEN
	slewtime := ReadParameter.Value;
END_IF

IF bSoEReset THEN
	fbSoEReset(
		Axis := axis_ref,
		Execute := TRUE,
		Timeout := DEFAULT_ADS_TIMEOUT);
 	IF NOT fbSoEReset.Busy THEN
		fbSoEReset(Axis := axis_ref, Execute := FALSE);
		bSoEReset := FALSE;
		Reset := TRUE;
	END_IF
END_IF

derotator_torque := torque / 10.0;
// nominal torque is 3%, max is 25%
TorqueError(IN := ABS(derotator_torque) > 20.0);

DerotatorEvent(	
	Trigger 		:= Error, 
	Level 			:= ADSLOG_MSGTYPE_ERROR,
	FormatString	:= 'Derotator Axis Error: %s',
	OnMessage 		:= NCError_TO_STRING(ErrorID),
	OffMEssage 		:= 'OK',
	OffLevel 		:= ADSLOG_MSGTYPE_HINT,
	Comm			:= comm);
	
DerotatorDiagnosticEvent(	
	Trigger 		:= DiagnosticError, 
	Level 			:= ADSLOG_MSGTYPE_ERROR,
	FormatString	:= 'Derotator Axis Diagnostic Error: %s',
	OnMessage 		:= NCError_TO_STRING(diagnostic),
	Comm			:= comm);
	
TorqueWarning(	
	Trigger 		:= TorqueError.Q, 
	Level 			:= ADSLOG_MSGTYPE_WARN,
	FormatString 	:= '%s',
	OnMessage 		:= 'Derotator torque is high.',
	Comm			:= comm);

CalibrationEvent(	
	Trigger 		:= Calibrated, 
	Level 			:= ADSLOG_MSGTYPE_HINT,
	OnMessage 		:= 'Derotator is calibrated.',
	Comm			:= comm);
	]]></ST>
    </Implementation>
    <Method Name="FB_Init" Id="{17d81d64-5647-49eb-8f39-4de9c4a6dd83}">
      <Declaration><![CDATA[//FB_Init is always available implicitly and it is used primarily for initialization.
//The return value is not evaluated. For a specific influence, you can also declare the
//methods explicitly and provide additional code there with the standard initialization
//code. You can evaluate the return value.
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains	: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode		: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)
	comm			: I_Comm;   
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.comm := comm;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsInMaxLimit" Id="{85224394-9a32-4685-b3cf-6a13bc098736}">
      <Declaration><![CDATA[METHOD PUBLIC IsInMaxLimit : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsInMaxLimit := digital_inputs.0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsInMinLimit" Id="{62606f0a-fd18-40f4-8386-8e94886290d7}">
      <Declaration><![CDATA[METHOD PUBLIC IsInMinLimit : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsInMinLimit := digital_inputs.1;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsReady" Id="{8cc15e49-347f-420a-b869-3556a24cdf75}">
      <Declaration><![CDATA[METHOD PUBLIC IsReady : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsReady := ready;]]></ST>
      </Implementation>
    </Method>
    <Method Name="MoveLeft" Id="{f9da8fce-1e75-499f-9e0c-b87953d543dc}">
      <Declaration><![CDATA[METHOD PUBLIC MoveLeft : BOOL
VAR_INPUT
	enable	: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[left := enable;]]></ST>
      </Implementation>
    </Method>
    <Method Name="MoveRight" Id="{36c00668-a0dc-4b1f-8735-c26e99bf4c2c}">
      <Declaration><![CDATA[METHOD PUBLIC MoveRight : BOOL
VAR_INPUT
	enable	: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[right := enable;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_DerotatorControl">
      <LineId Id="1" Count="126" />
    </LineIds>
    <LineIds Name="FB_DerotatorControl.FB_Init">
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_DerotatorControl.IsInMaxLimit">
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_DerotatorControl.IsInMinLimit">
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_DerotatorControl.IsReady">
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_DerotatorControl.MoveLeft">
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_DerotatorControl.MoveRight">
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>